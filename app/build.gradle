plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    startVersion = 100000000 // 100,000,000
    versionMajor = 1
    versionMinor = 2
    versionPatch = 32
}

@SuppressWarnings('GrMethodMayBeStatic')
private def generateVersionCode() {
    return (startVersion
            + versionMajor * 10000
            + versionMinor * 100
            + versionPatch)
}

@SuppressWarnings('GrMethodMayBeStatic')
private def generateVersionName() {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

android {
    namespace 'com.tunetide.music'
    compileSdk 33

    defaultConfig {
        applicationId "com.tunetide.music"
        minSdk 28
        targetSdk 33
        versionCode = generateVersionCode()
        versionName = generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            versionNameSuffix '.(DEBUG)'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    kotlinOptions {
        jvmTarget = '19'
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Music-${variant.versionName}.apk"
        }
    }
}

dependencies {
    //firebase
    implementation(platform("com.google.firebase:firebase-bom:31.3.0"))
    implementation("com.google.firebase:firebase-analytics")
    implementation("com.google.firebase:firebase-crashlytics")

    implementation 'com.github.bumptech.glide:glide:4.16.0'

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "com.google.dagger:hilt-android:2.50"
    kapt "com.google.dagger:hilt-compiler:2.50"

    implementation "androidx.activity:activity-ktx:1.6.0"

    def nav_version = "2.6.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def lifecycle_version = "2.6.0"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    // Annotation processor
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.6.0"

    def media_version = "1.1.1"
    implementation "androidx.media3:media3-exoplayer:$media_version"
    implementation "androidx.media3:media3-ui:$media_version"
    implementation "androidx.media3:media3-common:$media_version"
    implementation "androidx.media3:media3-session:$media_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.0"

    def paging_version = "3.1.0"
    implementation "androidx.paging:paging-runtime:$paging_version"

    implementation 'androidx.core:core:1.8.0'
    implementation 'androidx.media:media:1.6.0'


}